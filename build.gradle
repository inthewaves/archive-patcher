ext {
    archivePatcherVersion = "1.0"
}

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'


sourceCompatibility = 1.8
targetCompatibility = 1.8

version = archivePatcherVersion

subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }
jar.dependsOn subprojects.tasks['classes']
jar {
    manifest {
        attributes 'Implementation-Title': 'Archive Patcher',
        	'Implementation-Version': version,
            'Implementation-Vendor': 'https://github.com/inthewaves/archive-patcher'
    }
    baseName = project.name
    subprojects.each { subproject ->
        from subproject.sourceSets.main.output
    }
    from "LICENSE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Implementation-Title': 'Archive Patcher',
        	'Implementation-Version': version,
            'Implementation-Vendor': 'https://github.com/inthewaves/archive-patcher'
    }
    baseName = project.name
    classifier = 'sources'
    subprojects.each { subproject ->
        from subproject.sourceSets.main.allSource
    }
    from "LICENSE"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    manifest {
        attributes 'Implementation-Title': 'Archive Patcher',
        	'Implementation-Version': version,
            'Implementation-Vendor': 'https://github.com/inthewaves/archive-patcher'
    }
    baseName = project.name
    classifier = 'javadoc'
    subprojects.each { subproject ->
        from subproject.javadoc.destinationDir
    }
    from "LICENSE"
}

dependencies {
    compile project(':tools')
}

allprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/inthewaves/archive-patcher")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GHACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GHTOKEN")
            }
        }
    }

    publications {
        archivePatcher(MavenPublication) {
            from components.java
            groupId 'pm.inthewaves'
            artifactId 'archive-patcher'
            version this.version

            pom {
                name = 'archive-patcher'
                description = 'Archive patcher library (fork)'
                url = 'https://github.com/inthewaves/archive-patcher'

                scm {
                    url = 'scm:git@github.com:inthewaves/archive-patcher.git'
                    connection = 'scm:git@github.com:inthewaves/archive-patcher.git'
                    developerConnection = 'scm:git@github.com:inthewaves/archive-patcher.git'
                }

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.html'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}